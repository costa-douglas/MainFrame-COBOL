      *================================================================*00001000
       IDENTIFICATION                            DIVISION.              00002000
      *================================================================*00002100
                                                                        00002200
       PROGRAM-ID.  F05CIP2.                                            00002300
       AUTHOR.      DOUGLAS.                                            00002401
                                                                        00002500
      *================================================================*00002600
      *                         F O U R S Y S                          *00002700
      *================================================================*00002800
      *    PROGRAMA...: F05CIP2                                        *00002900
      *    TIPO.......: SERVICO DE ACESSO A DADOS COM CICS             *00003000
      *----------------------------------------------------------------*00003100
      *    PROGAMADOR.: DOUGLAS COSTA                                  *00003200
      *    EMPRESA....: FOURSYS                                        *00003300
      *    ANALISTA...: IVAN SANCHES                                   *00003400
      *    DATA.......: 26/06/2022                                     *00003547
      *----------------------------------------------------------------*00003600
      *    OBJETIVO : MANIPULACAO E CONSULTA EM REGISTROS DA TABELA    *00003745
      *               DB2 (FUNC) POR MEIO DE MAPA BMS (F05CIMX).       *00003846
      *----------------------------------------------------------------*00003900
      *    BASE DE DADOS:                                              *00004046
      *       DDNAME                I/O                INCLUDE/BOOK    *00004146
      *    FOUR001.FUNC              I                   BOOKFUNC      *00004246
      *----------------------------------------------------------------*00004300
      *    MODULOS.......:                                             *00004400
      *     ------------                                               *00004546
      *----------------------------------------------------------------*00004600
      *    COPYBOOKS.....:                                             *00004746
      *     ------------                                               *00004846
      *================================================================*00004900
                                                                        00005000
      *================================================================*00005100
       ENVIRONMENT DIVISION.                                            00005200
      *================================================================*00005300
                                                                        00005400
      *----------------------------------------------------------------*00005500
       CONFIGURATION                              SECTION.              00005600
      *----------------------------------------------------------------*00005700
                                                                        00005800
       SPECIAL-NAMES.                                                   00005900
           DECIMAL-POINT IS COMMA.                                      00006000
                                                                        00006100
      *----------------------------------------------------------------*00006200
                                                                        00006300
      *================================================================*00006400
       DATA                DIVISION.                                    00006500
      *================================================================*00006600
                                                                        00006700
      *----------------------------------------------------------------*00006800
       WORKING-STORAGE                            SECTION.              00006900
      *----------------------------------------------------------------*00007000
                                                                        00007100
      *----------------------------------------------------------------*00007200
       01  FILLER                      PIC  X(050)         VALUE        00008000
                  '*** INICIO DA WORKING F05CIP2 ***'.                  00009000
      *----------------------------------------------------------------*00010000
                                                                        00011000
      *----------------------------------------------------------------*00011100
       01  FILLER                       PIC X(050)           VALUE      00011200
                       '*** AREA DE NULIDADES ***'.                     00011300
      *----------------------------------------------------------------*00011400
                                                                        00011500
       77 WRK-EMAIL-NULL                PIC S9(004)  COMP.              00011600
                                                                        00011700
      *----------------------------------------------------------------*00011800
       01  FILLER                       PIC X(050)           VALUE      00011900
                      '*** AREA DE AUXILIARES ***'.                     00012000
      *----------------------------------------------------------------*00012100
                                                                        00012200
       77 WRK-SQLCODE                   PIC -999.                       00012410
       77 WRK-FLAG                      PIC 9 VALUE ZEROS.              00012531
       77 WRK-SALARIO                   PIC S9(04).                     00015734
                                                                        00015807
      *----------------------------------------------------------------*00015934
       01  FILLER                       PIC X(050)           VALUE      00016034
                        '*** AREA DE DATA ***'.                         00016134
      *----------------------------------------------------------------*00016234
                                                                        00016334
       01 WRK-DATA1.                                                    00016438
          05 WRK-ANO                    PIC 9(04).                      00016538
          05 FILLER                     PIC X(01).                      00016638
          05 WRK-MES                    PIC 9(02).                      00016738
          05 FILLER                     PIC X(01).                      00016838
          05 WRK-DIA                    PIC 9(02).                      00016938
                                                                        00017034
       01 WRK-DATA2.                                                    00017138
          05 WRK-DIA                    PIC 9(02).                      00017238
          05 FILLER                     PIC X(01) VALUE '/'.            00017338
          05 WRK-MES                    PIC 9(02).                      00017438
          05 FILLER                     PIC X(01) VALUE '/'.            00017538
          05 WRK-ANO                    PIC 9(04).                      00017638
                                                                        00017738
      *----------------------------------------------------------------*00017800
       01  FILLER                       PIC X(050)           VALUE      00017900
                        '*** AREA F05CIMX ***'.                         00018000
      *----------------------------------------------------------------*00018100
                                                                        00018200
           COPY F05CIMX.                                                00018300
           COPY DFHAID.                                                 00018413
                                                                        00018516
      *----------------------------------------------------------------*00018600
       01  FILLER                       PIC X(050)           VALUE      00018700
                           '*** AREA DB2 ***'.                          00018800
      *----------------------------------------------------------------*00018900
                                                                        00019000
           EXEC SQL                                                     00019100
            INCLUDE BOOKFUNC                                            00019200
           END-EXEC.                                                    00019300
                                                                        00019400
           EXEC SQL                                                     00019500
            INCLUDE SQLCA                                               00019600
           END-EXEC.                                                    00019700
                                                                        00019800
      *----------------------------------------------------------------*00019900
       01  FILLER                      PIC  X(050)         VALUE        00020000
               '*** F05CIP2 - FIM DA AREA DE WORKING ***'.              00020100
      *----------------------------------------------------------------*00020200
                                                                        00021000
      *================================================================*00022000
        PROCEDURE                       DIVISION.                       00023000
      *================================================================*00024000
                                                                        00025000
      ******************************************************************00025100
      *                    P R I N C I P A L                           *00025200
      ******************************************************************00025300
                                                                        00025400
      *----------------------------------------------------------------*00025500
       0000-PRINCIPAL                            SECTION.               00025600
      *----------------------------------------------------------------*00025700
                                                                        00025800
           PERFORM 1000-INICIAR.                                        00025900
                                                                        00026000
           PERFORM 2000-PROCESSAR.                                      00026100
                                                                        00026200
           PERFORM 3000-FINALIZAR.                                      00026300
                                                                        00026400
                                                                        00026500
      *----------------------------------------------------------------*00026600
       0000-99-FIM.                           EXIT.                     00026700
      *----------------------------------------------------------------*00026800
                                                                        00026900
      ******************************************************************00027000
      *                      I N I C I A R                             *00028000
      ******************************************************************00029000
                                                                        00030000
      *----------------------------------------------------------------*00040000
       1000-INICIAR                           SECTION.                  00041000
      *----------------------------------------------------------------*00041100
                                                                        00041200
             EXEC CICS SEND                                             00041300
               MAPSET('F05CIMX')                                        00041400
               MAP('MAPTEST')                                           00041500
               ERASE                                                    00041600
               MAPONLY                                                  00041700
             END-EXEC.                                                  00041800
                                                                        00041900
      *----------------------------------------------------------------*00042700
       1000-99-FIM.                          EXIT.                      00043000
      *----------------------------------------------------------------*00043100
                                                                        00043200
      ******************************************************************00043300
      *                     P R O C E S S A R                          *00043407
      ******************************************************************00043500
                                                                        00043600
      *----------------------------------------------------------------*00043700
       2000-PROCESSAR                       SECTION.                    00043800
      *----------------------------------------------------------------*00043900
                                                                        00045400
           EXEC CICS RECEIVE                                            00045500
            MAPSET('F05CIMX')                                           00045600
            MAP('MAPTEST')                                              00045700
            INTO(MAPTESTI)                                              00045800
           END-EXEC.                                                    00045900
                                                                        00046002
      *----------------------------------------------------------------*00046102
      *              R O T I N A  D E  S A I D A - F 3                 *00046202
      *----------------------------------------------------------------*00046302
                                                                        00046402
           IF EIBAID = DFHPF3                                           00046514
              EXEC CICS                                                 00046602
               RETURN                                                   00046702
              END-EXEC                                                  00046802
           END-IF.                                                      00046902
                                                                        00047007
      *----------------------------------------------------------------*00047104
      *           R O T I N A  D E  I N S E R T  - F 5                 *00047204
      *----------------------------------------------------------------*00047304
                                                                        00047402
           IF EIBAID = DFHPF5                                           00047514
              MOVE IDI            TO DB2-ID                             00047604
              MOVE NOMEI          TO DB2-NOME                           00047704
              MOVE SETORI         TO DB2-SETOR                          00047804
              MOVE SALI           TO DB2-SALARIO                        00047905
              MOVE DATADMI        TO DB2-DATAADM                        00048044
              MOVE EMAILI         TO DB2-EMAIL                          00048904
           EXEC SQL                                                     00049004
             INSERT INTO FOUR001.FUNC(ID,NOME,SETOR,SALARIO,            00049112
                                      DATAADM,EMAIL)                    00049212
             VALUES(:DB2-ID,                                            00049304
                    :DB2-NOME,                                          00049404
                    :DB2-SETOR,                                         00049504
                    :DB2-SALARIO,                                       00049604
                    :DB2-DATAADM,                                       00049704
                    :DB2-EMAIL )                                        00049804
           END-EXEC                                                     00049904
                                                                        00050034
              EVALUATE SQLCODE                                          00050104
               WHEN 0                                                   00050204
                                                                        00050334
                EXEC SQL                                                00050404
                  COMMIT                                                00050504
                END-EXEC                                                00050604
                                                                        00050734
                 MOVE 'USUARIO INSERIDO COM SUCESSO' TO MSGO            00050804
               WHEN -180                                                00050934
                 MOVE SQLCODE                        TO WRK-SQLCODE     00051034
                 MOVE 'DATA INVALIDA: '              TO MSGO            00051134
                 MOVE WRK-SQLCODE                    TO MSGO(16:04)     00051234
               WHEN -803                                                00051334
                 MOVE SQLCODE                        TO WRK-SQLCODE     00051434
                 MOVE 'REGITRO DUPLICADO: '          TO MSGO            00051534
                 MOVE WRK-SQLCODE                    TO MSGO(20:04)     00051634
               WHEN OTHER                                               00051715
                 MOVE SQLCODE                        TO WRK-SQLCODE     00051835
                 MOVE 'ERRO : '                      TO MSGO            00051935
                 MOVE WRK-SQLCODE                    TO MSGO(07:04)     00052035
              END-EVALUATE                                              00052104
           END-IF.                                                      00052204
                                                                        00052315
      *----------------------------------------------------------------*00052402
      *           R O T I N A  D E  C O N S U L T A - F 6              *00052502
      *----------------------------------------------------------------*00052602
           IF EIBAID = DFHPF6                                           00052714
                                                                        00052802
                MOVE IDI TO DB2-ID                                      00052922
                                                                        00053000
                EXEC SQL                                                00053102
                  SELECT ID,NOME,SETOR,SALARIO,DATAADM,EMAIL            00053202
                    INTO :DB2-ID,                                       00053302
                         :DB2-NOME,                                     00053402
                         :DB2-SETOR,                                    00053502
                         :DB2-SALARIO,                                  00053602
                         :DB2-DATAADM,                                  00053702
                         :DB2-EMAIL  :WRK-EMAIL-NULL                    00053802
                    FROM FOUR001.FUNC                                   00053902
                    WHERE ID = :DB2-ID                                  00054002
                     FETCH FIRST ROW ONLY                               00054102
                END-EXEC                                                00054221
                                                                        00054300
                EVALUATE SQLCODE                                        00054402
                 WHEN 0                                                 00054502
                  MOVE DB2-ID                TO IDO                     00054617
                  MOVE DB2-NOME              TO NOMEO                   00054717
                  MOVE DB2-SETOR             TO SETORO                  00054817
                  MOVE DB2-SALARIO           TO SALO                    00054917
                  MOVE DB2-DATAADM           TO WRK-DATA1               00055038
                  MOVE WRK-DIA OF WRK-DATA1  TO WRK-DIA                 00055138
                                             OF WRK-DATA2               00055238
                  MOVE WRK-MES OF WRK-DATA1  TO WRK-MES                 00055338
                                             OF WRK-DATA2               00055438
                  MOVE WRK-ANO OF WRK-DATA1  TO WRK-ANO                 00055538
                                             OF WRK-DATA2               00055638
                  MOVE WRK-DATA2             TO DATADMO                 00055739
                                                                        00055834
                    IF WRK-EMAIL-NULL          EQUAL -1                 00055903
                     MOVE '--EMAIL NAO ENCONTRADO--' TO EMAILO          00056003
                    ELSE                                                00056103
                     MOVE DB2-EMAIL             TO EMAILO               00056203
                    END-IF                                              00056303
                                                                        00056403
                  MOVE '** ENCONTRADO **'    TO MSGO                    00056503
                                                                        00056600
                 WHEN 100                                               00056703
                  INITIALIZE MAPTESTO                                   00056803
                  MOVE 'NAO ENCONTRADO '     TO MSGO                    00056903
                                                                        00057034
                 WHEN OTHER                                             00057103
                  MOVE SQLCODE               TO WRK-SQLCODE             00057217
                  MOVE 'ERRO: '              TO MSGO                    00057317
                  MOVE WRK-SQLCODE           TO MSGO(07:04)             00057417
                END-EVALUATE                                            00057520
             END-IF.                                                    00057620
      *----------------------------------------------------------------*00057708
      *           R O T I N A  D E  U P D A T E  - F 9                 *00057808
      *----------------------------------------------------------------*00057908
                                                                        00058008
           IF EIBAID = DFHPF9                                           00058114
                                                                        00058224
           MOVE IDI               TO DB2-ID                             00058324
                                                                        00059031
                EXEC SQL                                                00059331
                  SELECT ID,NOME,SETOR,SALARIO,DATAADM,EMAIL            00059431
                    INTO :DB2-ID,                                       00059531
                         :DB2-NOME,                                     00059631
                         :DB2-SETOR,                                    00059731
                         :DB2-SALARIO,                                  00059831
                         :DB2-DATAADM,                                  00059931
                         :DB2-EMAIL  :WRK-EMAIL-NULL                    00060031
                    FROM FOUR001.FUNC                                   00060131
                    WHERE ID = :DB2-ID                                  00060231
                    FETCH FIRST ROW ONLY                                00060331
                END-EXEC                                                00060431
                                                                        00060524
             EVALUATE SQLCODE                                           00060631
              WHEN 0                                                    00060731
                IF DB2-NOME            NOT EQUAL                        00060831
                   NOMEI               AND                              00060931
                   NOMEL               GREATER ZEROS                    00061031
                   MOVE NOMEI          TO DB2-NOME                      00061131
                END-IF                                                  00061231
                                                                        00061331
                IF DB2-SETOR           NOT EQUAL                        00061431
                   SETORI              AND                              00061531
                   SETORL              GREATER ZEROS                    00061631
                   MOVE SETORI         TO DB2-SETOR                     00061733
                END-IF                                                  00061831
                                                                        00061931
                MOVE DB2-SALARIO       TO WRK-SALARIO                   00062034
                IF WRK-SALARIO         NOT EQUAL SALI                   00062134
                                       AND                              00062234
                SALL GREATER ZEROS                                      00062334
                    MOVE SALI          TO DB2-SALARIO                   00062534
                ELSE                                                    00062634
                    MOVE 0             TO DB2-SALARIO                   00062734
                END-IF                                                  00062834
                                                                        00062931
                IF DB2-DATAADM         NOT EQUAL                        00063034
                   DATADMI             AND                              00063134
                   DATADMI             GREATER ZEROS                    00063234
                MOVE DATADMI           TO DB2-DATAADM                   00063334
                END-IF                                                  00063434
                                                                        00063532
                IF DB2-EMAIL           NOT EQUAL                        00063634
                   EMAILI              AND                              00063734
                   EMAILL              GREATER ZEROS                    00063834
                MOVE EMAILI            TO DB2-EMAIL                     00063934
                END-IF                                                  00064034
                                                                        00064132
                MOVE 1                 TO WRK-FLAG                      00064234
                                                                        00064334
                WHEN 100                                                00064432
                  MOVE 'REGISTRO INEXISTENTE' TO MSGO                   00064532
             END-EVALUATE                                               00064632
                                                                        00064732
             IF WRK-FLAG               EQUAL 1                          00064832
             EXEC SQL                                                   00064926
             UPDATE FOUR001.FUNC                                        00065026
             SET  NOME    =:DB2-NOME,                                   00065126
                  SETOR   =:DB2-SETOR,                                  00065226
                  SALARIO =:DB2-SALARIO,                                00065326
                  DATAADM =:DB2-DATAADM,                                00065426
                  EMAIL   =:DB2-EMAIL                                   00065526
             WHERE ID     =:DB2-ID                                      00065626
            END-EXEC                                                    00065726
                                                                        00065808
            IF SQLCODE EQUAL ZEROS                                      00065932
             EXEC SQL                                                   00066032
               COMMIT                                                   00066132
             END-EXEC                                                   00066232
               MOVE 'REGISTRO ALTERADO COM SUCESSO' TO MSGO             00066317
            ELSE                                                        00066432
               MOVE SQLCODE                         TO WRK-SQLCODE      00066517
               MOVE WRK-SQLCODE                     TO MSGO             00066632
            END-IF                                                      00066732
            MOVE 0                                  TO WRK-FLAG         00066832
           END-IF.                                                      00066908
                                                                        00067008
      *----------------------------------------------------------------*00067107
      *           R O T I N A  D E  D E L E T A R - F 10               *00067207
      *----------------------------------------------------------------*00067307
                                                                        00067400
           IF EIBAID = DFHPF10                                          00067514
                                                                        00067607
              MOVE IDI               TO DB2-ID                          00067707
                                                                        00067808
           EXEC SQL                                                     00067908
            DELETE FROM FOUR001.FUNC                                    00068008
            WHERE ID = :DB2-ID                                          00068108
           END-EXEC                                                     00068208
                                                                        00068308
            EVALUATE SQLCODE                                            00068408
             WHEN 0                                                     00068508
              EXEC SQL                                                  00068615
                COMMIT                                                  00068715
              END-EXEC                                                  00068815
               MOVE 'FUNCIONARIO EXCLUIDO COM SUCESSO' TO MSGO          00068917
             WHEN 100                                                   00069008
               MOVE 'FUNCIONARIO INEXISTENTE - '       TO MSGO          00069134
             WHEN OTHER                                                 00069208
               MOVE SQLCODE                            TO WRK-SQLCODE   00069317
               MOVE 'ERRO: '                           TO MSGO          00069417
               MOVE WRK-SQLCODE                        TO MSGO(07:04)   00069517
            END-EVALUATE                                                00069608
                                                                        00069708
           END-IF.                                                      00069808
                                                                        00069908
                                                                        00070008
           EXEC CICS SEND                                               00070100
              MAPSET('F05CIMX')                                         00070200
              MAP('MAPTEST')                                            00070300
              DATAONLY                                                  00070400
              FROM(MAPTESTO)                                            00070500
           END-EXEC.                                                    00070600
                                                                        00070700
      *----------------------------------------------------------------*00070800
       2000-99-FIM.                            EXIT.                    00070900
      *----------------------------------------------------------------*00071000
                                                                        00071100
      ******************************************************************00071200
      *                    F I N A L I Z A R                           *00071300
      ******************************************************************00071400
                                                                        00071500
      *----------------------------------------------------------------*00071600
       3000-FINALIZAR                        SECTION.                   00071700
      *----------------------------------------------------------------*00071800
                                                                        00071900
           EXEC CICS                                                    00072000
            RETURN TRANSID('T052')                                      00072100
           END-EXEC.                                                    00072200
                                                                        00072300
      *----------------------------------------------------------------*00072400
       3000-99-FIM.                           EXIT.                     00072500
      *----------------------------------------------------------------*00073000
                                                                        00080000
