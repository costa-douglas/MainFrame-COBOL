      *================================================================*00001001
       IDENTIFICATION                            DIVISION.              00002001
      *================================================================*00002101
                                                                        00002201
       PROGRAM-ID.  FR05DB02.                                           00002301
       AUTHOR.      DOUGLAS COSTA                                       00002401
                                                                        00002501
      *================================================================*00002601
      *                         F O U R S Y S                          *00002701
      *================================================================*00002801
      *    PROGRAMA...: FR05DB02                                       *00002901
      *    TIPO.......: SERVICO DE ACESSO A DADOS                      *00003001
      *----------------------------------------------------------------*00003101
      *    PROGAMADOR.: DOUGLAS COSTA                                  *00003201
      *    EMPRESA....: FOURSYS                                        *00003301
      *    ANALISTA...: IVAN SANCHES                                   *00003401
      *    DATA.......: 03/06/2022                                     *00003501
      *----------------------------------------------------------------*00003601
      *    OBJETIVO : ESTE PROGRAMA TEM A FINALIDADE DE RECEBER DADOS  *00003701
      *               DA TABELA FOUR001.FUNC                           *00003801
      *----------------------------------------------------------------*00003901
      *    BASE DE DADOS:                                              *00004001
      *      TABELAS DB2                           INCLUDE/BOOK        *00004101
      *      FOUR001.FUNC                           BOOKFUNC           *00004201
      ******************************************************************00004901
      *================================================================*00005001
                                                                        00005101
      *================================================================*00005201
       ENVIRONMENT DIVISION.                                            00005301
      *================================================================*00005401
                                                                        00005501
      *----------------------------------------------------------------*00005601
       CONFIGURATION                              SECTION.              00005701
      *----------------------------------------------------------------*00005801
                                                                        00005901
       SPECIAL-NAMES.                                                   00006001
           DECIMAL-POINT IS COMMA.                                      00006101
                                                                        00006201
      *----------------------------------------------------------------*00006301
                                                                        00006401
      *================================================================*00006501
       DATA                DIVISION.                                    00006601
      *================================================================*00006701
      *----------------------------------------------------------------*00006801
       WORKING-STORAGE                            SECTION.              00006901
      *----------------------------------------------------------------*00007001
                                                                        00008001
      *----------------------------------------------------------------*00009001
       01  FILLER                      PIC  X(050)         VALUE        00010001
                 '*** INICIO DA WORKING FR05DB02 ***'.                  00011001
      *----------------------------------------------------------------*00012001
                                                                        00013001
      *----------------------------------------------------------------*00014001
       01  FILLER                       PIC X(050)           VALUE      00015001
                    '*** AREA DE AUXILIARES ***'.                       00016001
      *----------------------------------------------------------------*00017001
                                                                        00017101
       77 WRK-ID                 PIC 9(04).                             00018001
       77 WRK-SQLCODE            PIC -999.                              00019001
       77 WRK-INDICATOR          PIC S9(4) COMP VALUE ZEROS.            00020001
                                                                        00020102
       01 WRK-ACUM-SAL           PIC 9(08)V99.                          00021002
       01 WRK-MEDIA-SAL          PIC 9(08)V99.                          00021102
       01 WRK-MAX-SAL            PIC 9(08)V99.                          00021202
                                                                        00021301
      *----------------------------------------------------------------*00021401
       01  FILLER                       PIC X(050)           VALUE      00021501
                    '*** AREA DE ACUMULADORES ***'.                     00021602
      *----------------------------------------------------------------*00021701
                                                                        00021801
        77 ACUM-LIDOS            PIC 9(05) VALUE ZEROS.                 00023402
                                                                        00024201
      *----------------------------------------------------------------*00025001
       01  FILLER                       PIC X(050)           VALUE      00025101
                        '*** AREA DB2 ***'.                             00025201
      *----------------------------------------------------------------*00025301
                                                                        00025401
           EXEC SQL                                                     00025501
             INCLUDE BOOKFUNC                                           00025601
           END-EXEC.                                                    00025701
                                                                        00025801
           EXEC SQL                                                     00025901
               INCLUDE SQLCA                                            00026001
           END-EXEC.                                                    00026101
                                                                        00026201
           EXEC SQL                                                     00026301
              DECLARE CFUNC CURSOR FOR                                  00026401
               SELECT * FROM FOUR001.FUNC                               00026501
                ORDER BY NOME                                           00026601
           END-EXEC.                                                    00026701
                                                                        00026801
      *----------------------------------------------------------------*00026901
       01  FILLER                      PIC  X(050)         VALUE        00027001
              '*** FR05DB02 - FIM DA AREA DE WORKING ***'.              00027101
      *----------------------------------------------------------------*00027201
                                                                        00027301
      *================================================================*00027401
        PROCEDURE                       DIVISION.                       00027501
      *================================================================*00027601
                                                                        00027701
      ******************************************************************00027801
      *                    P R I N C I P A L                           *00027901
      ******************************************************************00028001
                                                                        00028101
      *----------------------------------------------------------------*00029001
       0000-PRINCIPAL                            SECTION.               00030001
      *----------------------------------------------------------------*00040001
                                                                        00041001
           PERFORM 1000-INICIAR.                                        00041101
                                                                        00041201
           PERFORM 2000-PROCESSAR UNTIL SQLCODE EQUAL 100.              00041301
                                                                        00041401
           PERFORM 3000-FINALIZAR.                                      00041701
                                                                        00041901
           STOP RUN.                                                    00042001
                                                                        00042101
      *----------------------------------------------------------------*00042201
       0000-99-FIM.                           EXIT.                     00042301
      *----------------------------------------------------------------*00042401
                                                                        00042501
      ******************************************************************00042601
      *                      I N I C I A R                             *00042701
      ******************************************************************00042801
                                                                        00042901
      *----------------------------------------------------------------*00043001
       1000-INICIAR                           SECTION.                  00043101
      *----------------------------------------------------------------*00043201
                                                                        00043301
            EXEC SQL                                                    00043401
               OPEN CFUNC                                               00043501
            END-EXEC.                                                   00043601
                                                                        00043701
            EVALUATE SQLCODE                                            00043801
             WHEN 0                                                     00043901
               PERFORM 1100-LER-FUNCIONARIO                             00044002
             WHEN 100                                                   00044101
               DISPLAY 'SEM FUNCIONARIOS'                               00044202
             WHEN OTHER                                                 00044301
               MOVE SQLCODE              TO WRK-SQLCODE                 00044402
               DISPLAY 'ERRO ' WRK-SQLCODE 'NO OPEN CURSOR'             00044502
               MOVE 200                  TO RETURN-CODE                 00044602
               STOP RUN                                                 00044902
            END-EVALUATE.                                               00045001
                                                                        00045101
      *----------------------------------------------------------------*00045301
       1000-99-FIM.                              EXIT.                  00045401
      *----------------------------------------------------------------*00045501
                                                                        00045601
      *----------------------------------------------------------------*00045702
       1100-LER-FUNCIONARIO                   SECTION.                  00045802
      *----------------------------------------------------------------*00045902
                                                                        00046002
           EXEC SQL                                                     00046102
             FETCH CFUNC                                                00046202
              INTO :DB2-ID,                                             00046302
                   :DB2-NOME,                                           00046402
                   :DB2-SETOR,                                          00046502
                   :DB2-SALARIO,                                        00046602
                   :DB2-DATAADM,                                        00046704
                   :DB2-EMAIL    :WRK-INDICATOR                         00046802
           END-EXEC.                                                    00046902
                                                                        00047002
           EVALUATE SQLCODE                                             00048002
            WHEN 0                                                      00049002
              CONTINUE                                                  00050002
              ADD 1 TO ACUM-LIDOS                                       00051002
              COMPUTE WRK-ACUM-SAL = WRK-ACUM-SAL + DB2-SALARIO         00052002
              IF DB2-SALARIO      GREATER WRK-MAX-SAL                   00053004
                 MOVE DB2-SALARIO TO WRK-MAX-SAL                        00053104
              END-IF                                                    00053202
                                                                        00053302
            WHEN 100                                                    00053402
              DISPLAY 'FINAL DA TABELA'                                 00053502
            WHEN OTHER                                                  00053602
              MOVE SQLCODE TO WRK-SQLCODE                               00053702
              DISPLAY 'ERRO ...' WRK-SQLCODE                            00053802
           END-EVALUATE.                                                00053902
                                                                        00054002
      *----------------------------------------------------------------*00054101
       1100-99-FIM.                           EXIT.                     00054202
      *----------------------------------------------------------------*00054301
      ******************************************************************00054402
      *                   P R O C E S S A R                            *00054502
      ******************************************************************00054602
      *----------------------------------------------------------------*00055002
       2000-PROCESSAR                        SECTION.                   00055102
      *----------------------------------------------------------------*00055202
                                                                        00055302
           COMPUTE WRK-MEDIA-SAL = WRK-ACUM-SAL / ACUM-LIDOS            00055402
                                                                        00055502
           DISPLAY '---------------------------------------'            00055602
           DISPLAY '              RESGISTROS               '            00055702
           DISPLAY '---------------------------------------'            00055802
           DISPLAY 'ID.....: ' DB2-ID                                   00055902
           DISPLAY 'NOME...: ' DB2-NOME                                 00056002
           DISPLAY 'SETOR..: ' DB2-SETOR                                00056102
           DISPLAY 'SALARIO: ' DB2-SALARIO                              00056202
           DISPLAY 'DATAADM: ' DB2-DATAADM                              00056302
           DISPLAY '---------------------------------------'            00056402
                                                                        00056502
           IF WRK-INDICATOR = 0                                         00056602
              DISPLAY 'EMAIL...: ' DB2-EMAIL                            00056702
           ELSE                                                         00056802
              DISPLAY '-- SEM EMAIL CADASTRADO --'                      00056902
           END-IF                                                       00057002
                                                                        00057102
           PERFORM 1100-LER-FUNCIONARIO.                                00057202
                                                                        00057302
      *----------------------------------------------------------------*00057402
       2000-99-FIM.                           EXIT.                     00057502
      *----------------------------------------------------------------*00057602
                                                                        00057701
      ******************************************************************00057801
      *                 F I N A L I Z A C A O                          *00058001
      ******************************************************************00060001
                                                                        00070001
      *----------------------------------------------------------------*00071001
       3000-FINALIZAR                        SECTION.                   00072002
      *----------------------------------------------------------------*00073001
                                                                        00073103
            DISPLAY '----------------------------------'                00073203
            DISPLAY '          F R 0 5 D B 0 2         '                00073303
            DISPLAY '----------------------------------'                00073403
            DISPLAY 'MAIOR SALARIO: ' WRK-MAX-SAL                       00073503
            DISPLAY 'MEDIA SALARIO: ' WRK-MEDIA-SAL                     00073603
            DISPLAY 'TOTAL SALARIO: ' WRK-ACUM-SAL                      00073703
            DISPLAY '----------------------------------'                00073803
                                                                        00073903
            EXEC SQL                                                    00074001
              CLOSE CFUNC                                               00074101
            END-EXEC.                                                   00074201
                                                                        00076001
      *----------------------------------------------------------------*00077001
       3000-99-FIM.                           EXIT.                     00077102
      *----------------------------------------------------------------*00077201
