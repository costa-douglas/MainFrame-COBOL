      *================================================================*00001000
       IDENTIFICATION                            DIVISION.              00002000
      *================================================================*00002100
                                                                        00002200
       PROGRAM-ID.  FR05DB11.                                           00002301
       AUTHOR.      DOUGLAS COSTA                                       00002400
                                                                        00002500
      *================================================================*00002600
      *                         F O U R S Y S                          *00002700
      *================================================================*00002800
      *    PROGRAMA...: FR05DB11                                       *00002901
      *    TIPO.......: SERVICO DE ACESSO A DADOS                      *00003000
      *----------------------------------------------------------------*00003100
      *    PROGAMADOR.: DOUGLAS COSTA                                  *00003200
      *    EMPRESA....: FOURSYS                                        *00003300
      *    ANALISTA...: IVAN SANCHES                                   *00003400
      *    DATA.......: 09/06/2022                                     *00003501
      *----------------------------------------------------------------*00003600
      *    OBJETIVO...: ESTE PROGRAMA TEM A FINALIDADE DE RECEBER DADOS*00003704
      *                 DA TABELA FOUR001.FUNC E GRAVAR LAYOUT DE SAIDA*00003804
      *                 NO ARQUIVO RELDB2.                             *00003904
      *----------------------------------------------------------------*00004000
      *    ARQUIVOS...:                                                *00004104
      *     DDNAME                 I/O                                 *00004203
      *     RELDB2                  O                                  *00004303
      *----------------------------------------------------------------*00004402
      *    BASE DE DADOS:                                              *00004500
      *     TABELAS DB2                            INCLUDE/BOOK        *00004603
      *     FOUR001.FUNC                             BOOKFUNC          *00004703
      *----------------------------------------------------------------*00004803
      *    COPYBOOKS..:                                                *00004904
      *     B#GRAVDB  -  AREA DE TRATAMENTO DE ERROS                   *00005004
      *----------------------------------------------------------------*00005104
      ******************************************************************00005200
                                                                        00005304
      *================================================================*00005400
                                                                        00005500
      *================================================================*00005600
       ENVIRONMENT DIVISION.                                            00005700
      *================================================================*00005800
                                                                        00005900
      *----------------------------------------------------------------*00006000
       CONFIGURATION                              SECTION.              00006100
      *----------------------------------------------------------------*00006200
                                                                        00006300
       SPECIAL-NAMES.                                                   00006400
           DECIMAL-POINT IS COMMA.                                      00006500
                                                                        00006600
      *----------------------------------------------------------------*00006700
      *----------------------------------------------------------------*00006804
       INPUT-OUTPUT                               SECTION.              00006904
      *----------------------------------------------------------------*00007004
                                                                        00007104
       FILE-CONTROL.                                                    00007204
             SELECT RELDB2 ASSIGN         TO RELDB2                     00007304
                    FILE STATUS           IS WRK-FS-RELDB2.             00007421
                                                                        00007504
      *----------------------------------------------------------------*00007604
      *================================================================*00007700
       DATA                                       DIVISION.             00007804
      *================================================================*00007900
                                                                        00008004
      *----------------------------------------------------------------*00008104
       FILE                                       SECTION.              00008204
      *----------------------------------------------------------------*00008304
      *----------------------------------------------------------------*00008404
      *    OUTPUT - DADOS DO ARQUIVO DE SAIDA (RELDB2) - LRECL = 098   *00008604
      *----------------------------------------------------------------*00008700
                                                                        00008804
       FD RELDB2                                                        00008904
           RECORDING MODE IS F                                          00009004
           LABEL RECORD IS STANDARD                                     00009104
           BLOCK CONTAINS 0 RECORDS.                                    00009204
                                                                        00009304
       01 FD-RELDB2                 PIC X(98).                          00009421
                                                                        00009504
      *----------------------------------------------------------------*00009604
       WORKING-STORAGE                            SECTION.              00009700
      *----------------------------------------------------------------*00009800
                                                                        00009900
      *----------------------------------------------------------------*00010000
       01  FILLER                      PIC  X(050)         VALUE        00010100
                 '*** INICIO DA WORKING FR05DB11 ***'.                  00010204
      *----------------------------------------------------------------*00010300
                                                                        00010400
      *----------------------------------------------------------------*00010505
       01  FILLER                      PIC  X(050)         VALUE        00010605
                 '*** VARIAVEL DE FILE STATUS  ***'.                    00010705
      *----------------------------------------------------------------*00010805
                                                                        00010905
       01 WRK-FS-RELDB2          PIC 9(02) VALUE ZEROS.                 00011023
                                                                        00011105
      *----------------------------------------------------------------*00011200
       01  FILLER                       PIC X(050)           VALUE      00011300
                    '*** AREA DE AUXILIARES ***'.                       00011400
      *----------------------------------------------------------------*00011500
                                                                        00012000
       77 WRK-ID                 PIC 9(04).                             00013000
                                                                        00020000
      *----------------------------------------------------------------*00020116
       01  FILLER                       PIC X(050)           VALUE      00020204
                    '*** AREA DE NULIDADE ***'.                         00020304
      *----------------------------------------------------------------*00020404
                                                                        00020604
       77 WRK-EMAIL-NULL         PIC S9(04) COMP VALUE ZEROS.           00020711
                                                                        00020816
      *----------------------------------------------------------------*00021016
       01  FILLER                       PIC X(050)           VALUE      00021100
                    '*** AREA DE ACUMULADORES ***'.                     00021216
      *----------------------------------------------------------------*00021316
                                                                        00021400
        77 ACUM-LIDOS            PIC 9(05) VALUE ZEROS.                 00021500
        77 ACUM-GRAVADOS         PIC 9(05) VALUE ZEROS.                 00021620
                                                                        00021700
      *----------------------------------------------------------------*00021804
       01  FILLER                       PIC X(050)           VALUE      00021904
                    '*** AREA DO GRAVALOG ***'.                         00022004
      *----------------------------------------------------------------*00022104
                                                                        00022204
           COPY 'B#GRAVDB'.                                             00022317
                                                                        00022404
      *----------------------------------------------------------------*00022504
       01  FILLER                       PIC X(050)           VALUE      00022604
                    '*** AREA DO RELDB3 ***'.                           00022704
      *----------------------------------------------------------------*00022804
                                                                        00022904
           COPY 'B#RLTDB2'.                                             00023018
                                                                        00023104
      *----------------------------------------------------------------*00023200
       01  FILLER                       PIC X(050)           VALUE      00023300
                        '*** AREA DB2 ***'.                             00023400
      *----------------------------------------------------------------*00023500
                                                                        00023600
           EXEC SQL                                                     00024000
             INCLUDE BOOKFUNC                                           00025000
           END-EXEC.                                                    00025100
                                                                        00025200
           EXEC SQL                                                     00025300
               INCLUDE SQLCA                                            00025400
           END-EXEC.                                                    00025500
                                                                        00025600
           EXEC SQL                                                     00025700
              DECLARE CFUNC CURSOR FOR                                  00025800
               SELECT * FROM FOUR001.FUNC                               00025900
                ORDER BY NOME                                           00026000
           END-EXEC.                                                    00026100
                                                                        00026200
      *----------------------------------------------------------------*00026300
       01  FILLER                      PIC  X(050)         VALUE        00026400
              '*** FR05DB11 - FIM DA AREA DE WORKING ***'.              00026504
      *----------------------------------------------------------------*00026600
                                                                        00026700
      *================================================================*00026800
        PROCEDURE                       DIVISION.                       00026900
      *================================================================*00027000
                                                                        00027100
      ******************************************************************00027200
      *                    P R I N C I P A L                           *00027300
      ******************************************************************00027400
                                                                        00027500
      *----------------------------------------------------------------*00027600
       0000-PRINCIPAL                            SECTION.               00027700
      *----------------------------------------------------------------*00027800
                                                                        00027900
           PERFORM 1000-INICIAR.                                        00028000
                                                                        00029000
           PERFORM 2000-PROCESSAR UNTIL SQLCODE EQUAL 100.              00030000
                                                                        00040000
           PERFORM 3000-FINALIZAR.                                      00041000
                                                                        00041100
           STOP RUN.                                                    00041200
                                                                        00041300
      *----------------------------------------------------------------*00041400
       0000-99-FIM.                           EXIT.                     00041500
      *----------------------------------------------------------------*00041600
                                                                        00041700
      ******************************************************************00041800
      *                      I N I C I A R                             *00041900
      ******************************************************************00042000
                                                                        00042100
      *----------------------------------------------------------------*00042200
       1000-INICIAR                           SECTION.                  00042300
      *----------------------------------------------------------------*00042400
                                                                        00042500
            EXEC SQL                                                    00042600
               OPEN CFUNC                                               00042700
            END-EXEC.                                                   00042800
                                                                        00042900
            OPEN OUTPUT RELDB2.                                         00043005
                                                                        00043105
            PERFORM 1100-TESTAR-FILE-STATUS.                            00043220
                                                                        00043324
            PERFORM 2200-LER-FUNCIONARIO.                               00043424
                                                                        00043524
      *----------------------------------------------------------------*00043605
       1000-99-FIM.                           EXIT.                     00043705
      *----------------------------------------------------------------*00043805
      ******************************************************************00043905
      *             T E S T A R  F I L E - S T A T U S                 *00044005
      ******************************************************************00044105
      *----------------------------------------------------------------*00044205
       1100-TESTAR-FILE-STATUS                SECTION.                  00044320
      *----------------------------------------------------------------*00044405
                                                                        00044505
           IF WRK-FS-RELDB2 NOT EQUAL ZEROS                             00044605
                MOVE 'FR05DB11'              TO WRK-PROGRAMA            00044705
                MOVE 'ERRO NO OPEN RELDB2'   TO WRK-MENSAGEM            00044805
                MOVE '2000'                  TO WRK-SECAO               00044905
                MOVE WRK-FS-RELDB2           TO WRK-STATUS              00045005
                PERFORM 9999-TRATAR-ERRO                                00045105
           END-IF.                                                      00045205
                                                                        00045305
      *----------------------------------------------------------------*00045405
       1100-99-FIM.                           EXIT.                     00045505
      *----------------------------------------------------------------*00045605
                                                                        00047000
      ******************************************************************00054200
      *                   P R O C E S S A R                            *00054300
      ******************************************************************00054400
      *----------------------------------------------------------------*00054500
       2000-PROCESSAR                        SECTION.                   00054600
      *----------------------------------------------------------------*00054700
                                                                        00054800
           PERFORM 2100-GRAVAR-SAIDA.                                   00054910
                                                                        00055010
      *----------------------------------------------------------------*00055210
       2000-99-FIM.                           EXIT.                     00055310
      *----------------------------------------------------------------*00055410
      *----------------------------------------------------------------*00055510
       2100-GRAVAR-SAIDA                     SECTION.                   00055610
      *----------------------------------------------------------------*00055710
                                                                        00055810
           IF WRK-EMAIL-NULL       EQUAL -1                             00055911
              MOVE SPACES          TO DB2-EMAIL                         00056007
           END-IF.                                                      00056107
                                                                        00056200
           MOVE DB2-ID             TO WRK-REG-ID.                       00056318
           MOVE DB2-NOME           TO WRK-REG-NOME.                     00056418
           MOVE DB2-SETOR          TO WRK-REG-SETOR.                    00056818
           MOVE DB2-EMAIL          TO WRK-REG-EMAIL.                    00056919
                                                                        00057011
           WRITE FD-RELDB2         FROM WRK-REG-RELDB2.                 00057118
           ADD 1                   TO ACUM-GRAVADOS                     00057211
                                                                        00057311
           PERFORM 2200-LER-FUNCIONARIO.                                00057410
                                                                        00057500
      *----------------------------------------------------------------*00057600
       2100-99-FIM.                         EXIT.                       00057710
      *----------------------------------------------------------------*00057800
      *----------------------------------------------------------------*00057910
       2200-LER-FUNCIONARIO                 SECTION.                    00058010
      *----------------------------------------------------------------*00058110
                                                                        00058200
           EXEC SQL                                                     00058310
             FETCH CFUNC                                                00058410
              INTO :DB2-ID,                                             00058510
                   :DB2-NOME,                                           00058610
                   :DB2-SETOR,                                          00058710
                   :DB2-SALARIO,                                        00058810
                   :DB2-DATAADM,                                        00058910
                   :DB2-EMAIL    :WRK-EMAIL-NULL                        00059011
           END-EXEC.                                                    00059110
                                                                        00059210
           IF (SQLCODE                 NOT EQUAL ZEROS AND +100) OR     00059310
              (SQLWARN0                EQUAL 'W')                       00059410
               MOVE 'FR05DB11'         TO WRK-PROGRAMA                  00059510
               MOVE 'ERRO NA LEITURA'  TO WRK-MENSAGEM                  00059610
               MOVE '1000'             TO WRK-SECAO                     00059710
               MOVE SQLCODE            TO WRK-SQLCODE                   00059810
               PERFORM 9999-TRATAR-ERRO                                 00059910
           END-IF.                                                      00060010
                                                                        00060110
           ADD 1                   TO ACUM-LIDOS.                       00060321
                                                                        00060610
                                                                        00061210
      *----------------------------------------------------------------*00061310
       2200-99-FIM.                         EXIT.                       00061410
      *----------------------------------------------------------------*00061510
      ******************************************************************00061600
      *                 F I N A L I Z A C A O                          *00061700
      ******************************************************************00061800
                                                                        00061900
      *----------------------------------------------------------------*00062000
       3000-FINALIZAR                        SECTION.                   00062100
      *----------------------------------------------------------------*00062200
                                                                        00073200
            EXEC SQL                                                    00073300
              CLOSE CFUNC                                               00073400
            END-EXEC.                                                   00073500
                                                                        00073600
            CLOSE RELDB2.                                               00073710
                                                                        00073810
            PERFORM 1100-TESTAR-FILE-STATUS.                            00073921
                                                                        00074011
      *----------------------------------------------------------------*00074100
       3000-99-FIM.                           EXIT.                     00074200
      *----------------------------------------------------------------*00075000
                                                                        00076010
      ******************************************************************00076110
      *                 T R A T A R   E R R O S                        *00076210
      ******************************************************************00076310
      *----------------------------------------------------------------*00077010
       9999-TRATAR-ERRO                      SECTION.                   00078019
      *----------------------------------------------------------------*00079010
                                                                        00080010
            CALL 'GRALOGDB'         USING WRK-LOG.                      00090013
                                                                        00100010
            GOBACK.                                                     00110010
                                                                        00120013
      *----------------------------------------------------------------*00160010
       9999-99-FIM.                           EXIT.                     00170010
      *----------------------------------------------------------------*00180010
